<letter>        = 'a'..'z'
                | 'A'..'Z'
                | '_'.

<digit>         = '0'..'9'.

<id>            = <letter> {<letter> | <digit>}.

<number>        = <digit> {<digit>}.

<decs>          = {<dec>}.

<dec>           = <typedec>
                | <vardec>
                | <fundec>.

<typedec>       = 'type' <id> '=' <typespec>.

<typespec>      = <id>
                | 'record' ['(' <id> ')'] <fields> 'end'
                | 'array' 'of' <typespec>
                | 'function' '(' <fields> ')' [':' <id>].

<fields>        = [<id> ':' <typespec> {',' <id> ':' <typespec> }].

<vardec>        = 'var' <id> '=' <exp>
                | 'var' <id> ':' <typespec> '=' <exp>
                | 'var' <id> ':' <typespec>.

<fundec>        = 'function' <id> '(' <fields> ')' [':' <typespec>] <block>.

<block>         = <decs> ['begin' <seq>] 'end'.

<seq>           = {<stm>}.

<exp>           = <disjunction>.

<stm>           = <lvalue> ':=' <exp>
                | 'if' <exp> 'then' <seq>
                  ['else' <seq>]
                  'end'
                | 'while' <exp> 'do' <seq> 'end'
                | 'for' <id> ':=' <exp> 'to' <exp> 'do' <seq> 'end'
                | 'break'
                | 'return' <exp>
                | <call>
                | <stm> ';'.

<disjunction>   = <conjunction>
                | <disjunction> 'or' <conjunction>.

<conjunction>   = <complement>
                | <conjunction> 'and' <complement>.
                
<complement>    = ['not'] <bool>.
                
<bool>          = <sum> [ <relop> <sum> ].

<relop>         = '='
                | '<>'
                | '<'
                | '<='
                | '>'
                | '>='.
                
<sum>           = <product>
                | <sum> <addop> <product>.

<addop>         = '+'
                | '-'.

<product>       = <factor>
                | <product> <mulop> <factor>.

<mulop>         = '*'
                | '/'
                | 'mod'.
                
<factor>        = <call>
                | 'nil'
                | 'true'
                | 'false'
                | '(' <exp> ')'
                | <number>
                | <string>
                | '-' <factor>
                | <call>
                | 'array' <exp> {',' <exp>} 'of' <typespec>
                | 'new' <id>.

<call>          = <lvalue>
                | <lvalue> '(' [<exp> {',' <exp>}] ')'.

<lvalue>        = <id>
                | <lvalue> '.' <id>
                | <lvalue> '[' <exp> { ',' <exp> } ']'.

