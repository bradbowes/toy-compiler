<letter>        = 'a'..'z'
                | 'A'..'Z'.

<digit>         = '0'..'9'.

<id>            = <letter> {<letter>|<digit>|'_'}.

<number>        = <digit> {<digit>}.

<decs>          = {<dec>}.

<dec>           = <typedec>
                | <vardec>
                | <fundec>.

<typedec>       = 'type' <id> '=' <typespec>.

<typespec>      = <id>
                | 'record' ['(' <id> ')'] <fields> 'end'
                | 'array' 'of' <typespec>
                | 'function' '(' <fields> ')' [':' <id>].

<fields>        = [<id> ':' <typespec> {',' <id> ':' <typespec> }].

<vardec>        = 'var' <id> '=' <exp>
                | 'var' <id> ':' <typespec> '=' <exp>
                | 'var' <id> ':' <typespec>.

<fundec>        = 'function' <id> '(' <fields> ')' [':' <id>] <block>.

<block>         = <decs> ['begin' <seq>] 'end'.

<seq>           = {<stm>}.

<exp>           = <disjunction>.

<stm>           = <lvalue> ':=' <exp>
                | 'if' <exp> 'then' <seq>
                  ['else' <seq>]
                  'end'
                | 'while' <exp> 'do' <seq> 'end'
                | 'for' <id> ':=' <exp> 'to' <exp> 'do' <seq> 'end'
                | 'break'
                | 'return' <exp>
                | <call>
                | <stm> ';'.

<disjunction>   = <conjunction>
                | <disjunction> 'or' <conjunction>.

<conjunction>   = <complement>
                | <conjunction> 'and' <complement>.
                
<complement>    = ['not'] <bool>.
                
<bool>          = <sum> [ <relop> <sum> ].

<relop>         = '='
                | '<>'
                | '<'
                | '<='
                | '>'
                | '>='.
                
<sum>           = <product>
                | <sum> <addlop> <product>.

<addop>         = '+'
                | '-'.

<product>       = <factor>
                | <product> <mulop> <factor>.

<mulop>         = '*'
                | '/'
                | 'mod'.
                
<lvalue>        = <id>
                | <lvalue> '.' <id>
                | <lvalue> '[' <exp> ']'.

<factor>        = <lvalue>
                | 'nil'
                | 'true'
                | 'false'
                | '(' <exp> ')'
                | <number>
                | <string>
                | '-' <factor>
                | <call>
                | 'new' <id> {'[' <exp> ']'}.

<call>          = <lvalue> '(' [<exp> {',' <exp>}] ')'.
